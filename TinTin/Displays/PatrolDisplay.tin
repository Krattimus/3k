#showme READING CYCLER DISPLAY
#class {patrolspanel} {open}

/***********************************************************************
	Renders a nice box into the Controllers panel for the patrol list
***********************************************************************/
#alias {.renderPatrols} 
{
	#draw {line} $chat[height]+$caption[height]+1 -$canvas[width] -$status[height]-2 -$canvas[width];
	#showme {<228>\u252C<088>} $chat[height]+$caption[height] -$canvas[width];
	#showme {<088>\u2534<088>} -$hpbar[height]-$gline[height]-2 -$canvas[width];
		
	#if {"$draw[canvas]" == "on"} 
	{
		#NOP Clear previous drawing;
		#screen clear square $chat[height]+$caption[height]+1 -$canvas[width]+1 -$status[height]-2 -1;

		#if {&{patrols[]} == 0} 
		{
			#var botareas 
			{
				<178>            No Controllers<088>
			};
			#var botheight 1;
		};
		#else
		{
				#var botheight &{patrols[]};
				#var botareas {};
				#local patrolID 0;
				#while {$patrolID < &{patrols[]}} 
				{
					#math {patrolID} {$patrolID + 1};
					#if {$patrolID == $currentPatrolID}
					{
						#format {patrolIDnum} {%+4s} {<128>$patrolID.<088>};
						#format {area_display} {%-12s} {<128>$patrols[$patrolID]<088>}
					};
					#else
					{
						#format {patrolIDnum} {%+4s} {<178>$patrolID.<088>};
						#format {area_display} {%-12s} {<148>$patrols[$patrolID]<088>}
					};

					#if {$patrolID == 1} 
					{
						#var botareas {$patrolIDnum $area_display}
					};
					#else
					{
						#var botareas {$botareas\n$patrolIDnum $area_display}
					}
				};

				#format {area_display} {%-13s} {<158>%1 is <168>%2<088>};

		};
		
		#draw green rounded box $chat[height]+$caption[height]+2 -$canvas[width]+2 $chat[height]+$caption[height]+4 -3 
		{
				<138>              $controllers    <088>
		};
		
		#draw green rounded box $chat[height]+$caption[height]+4 -$canvas[width]+2 $chat[height]+$caption[height]+5+$botheight -3 
		{
				$botareas
		};

		#showme {<228>\u251C} $chat[height]+$caption[height]+4 -$canvas[width]+2;
		#showme {<228>\u2524} $chat[height]+$caption[height]+4 -3;
	}
}

#event {VARIABLE UPDATE currentPatrolID} 
{
	#delay 0 .renderPatrols
}

#event {VARIABLE UPDATE patrols} 
{
	#delay 0 .renderPatrols
}

#class {patrolspanel} {close}
