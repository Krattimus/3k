#showme READING MONKS

#class {monk} {open}

/**********************************************************************************
	Data
**********************************************************************************/
#var monk[g2n][prev] 0;
#var monk[speed] {};
#var monk[power] {};
#var monk[chiFocus] {};
#var monk[palm] 0;
#var monk[pressure] 0;
#var monk[gxp][count] 50;
#var monk[gxp][average] 0;
#var monk[crits] {};
#var monk[stun] {};
#var monk[hide] {};
#var monk[hideThreshold] 2000;
#var monk[method] {Tiger};
#var monk[isActive] 1;
#var monk[dps] 0;
#var monk[dpsOn] {  };
#var monk[dpsOff] {  };
#var monk[toughHands] 0;
#var monk[commandIndex] 0;
#var monk[roundCommand] 0;
#var monk[roundNumber] -1;
#var monk[stall] 0;
#var monk[commands] { {0}{\(heal $char[ourCharacter]}{1}{hmheal}{2}{eat rice}{3}{drink soda}{4}{drink cerveza}{5}{\(protection}{6}{hmshield}{7}{chiforce 450} };

#list {monk[gxp][history][list]} create {};

.registerEvent onNewMipGline onNewMipGlineMonk;
.registerEvent onMobKilled onMobKilledMonk;
.registerEvent onNewCombatRound onNewCombatRoundMonk;



#alias .round
{
	#var monk[roundNumber] %1;
	#var monk[roundCommand] %2;
};

#alias .dpsOn
{
	#draw Yellow scroll line 1 1 1 80;
	#showme Switching to Dps Mode;
	#draw Yellow scroll line 1 1 1 80;

	$monk[dpsOn];
	
	#var monk[dps] 1;
};

#alias .dpsOff
{
	#draw Yellow scroll line 1 1 1 80;
	#showme Switching to Tank Mode with $monk[commands][$monk[commandIndex]];
	#draw Yellow scroll line 1 1 1 80;

	$monk[dpsOff];

	hotd;
	#var monk[dps] 0;
	.doShield;
};

#alias .doShield
{
	#send $monk[commands][$monk[commandIndex]];
	#math {monk[commandIndex]} {($monk[commandIndex]+1) % 5e}
};

/**********************************************************************************
	Kill trig
**********************************************************************************/
#alias {onMobKilledMonk}
{
	#if { $settings[shouldProcessCorpses] }
	{
		wrap;
	};

	#if { $my[hp][current] <= $my[hp][max] }
	{
		#send !hands;
	};
	
	#var monk[commandIndex] 0;
	#var monk[palm] 0;
	#var monk[pressure] 0;
};

/**********************************************************************************
	Read speed and power from guild score
**********************************************************************************/
#action {Combat Method: %1 Speed: %2 Power: %3 Chi Focus: %4}
{
	#local speed {};
	#local power {};

	#format speed {%p} {%2};
	#format power {%p} {%3};

	#var monk[speed] $speed;
	#var monk[power] $power;
};

/**********************************************************************************
	Read speed and power from guild score
**********************************************************************************/W
#action {HP: %d/%d PP: %d/%d Chi: %d/%d G2:{[\d\-]+} Mind: %d AE:%d/%d%% CF:%w} 
{
	#var {my[hp][current]}	{%1};
	#var {my[hp][max]}	{%2};
	#var {my[sp][current]}	{%3};
	#var {my[sp][max]}	{%4};
	#var {my[gp1][current]}	{%5};
	#var {my[gp1][max]}	{%6};
	#var {my[g2n]}		{%7};
	#var {my[gp2][current]}	{%8};
	#var {my[ae][current]}	{%9};
	#var {my[ae][reset]}	{%10};
	#var {my[cf]}		{%11}
};

/*************************************************************************
	Detect monk pressure
*************************************************************************/
#action {^You find a pressure point and bore down!$}
{
	#var monk[pressure] 1;
};

#action {You cannot pressure point attack that creature again!}
{
	#var monk[pressure] 1;
};

/*************************************************************************
	Detect monk palm
*************************************************************************/
#action {^You reach out and touch your foe, sending death bringing vibrations}
{
	#var monk[palm] 1;
};

#action {You sit down and begin to meditate}
{
	#var monk[palm] 0;
};

#action {You cannot re-palm this monster}
{
	#var monk[palm] 1;
};

#action {The vibrations have ceased on}
{
	#var monk[palm] 0;
};

#alias .toughHands
{
	#var monk[toughHands] %1;
};

/*************************************************************************************
	Round of combat
*************************************************************************************/
#alias onNewCombatRoundMonk
{
	#NOP { NON MONK? SOULD BE IN BASE CONTROLLER? };
	#if { $currentRoundID <= 1 }
	{
		#send !consider;
	};

	#if { $monk[isActive] }
	{
		#math {spNormalized} {($my[sp][current]*100 / $my[sp][max]*100)/100};
		#math {hpNormalized} {($my[hp][current]*100 / $my[hp][max]*100)/100};

		#NOP { Tough hands, auto send X hands per round for tough fights };
		#var toughToGo $monk[toughHands];
        #while { $toughToGo > 0 }
        {
			#send !hands;
            #math {toughToGo} {$toughToGo-1};
        };

		#NOP { If in tank mode cycle SP healing };
		#if { $monk[stall] > 0 }
		{
			#math {monk[stall]} {$monk[stall]-1};
		};

		#if { $monk[dps] == 0 && $monk[stall] <= 0 }
		{
			#if {$spNormalized <= 25 }
			{
				.doShield;
				#var monk[stall] 5;

				#draw Yellow scroll line 1 1 1 80;
				#showme SP Healing;
				#draw Yellow scroll line 1 1 1 80;
			};
		};

		#NOP { If .round was used, it will cast here };
		#if { $monk[roundNumber] == $currentRoundID }
		{
			#draw Yellow scroll line 1 1 1 80;
			#showme $monk[roundCommand];
			#draw Yellow scroll line 1 1 1 80;
			#send $monk[roundCommand];
			#var monk[roundNumber] -1;
			#var monk[roundCommand] { };
		};

		#NOP { Auto hands };
		#if { $hpNormalized < 90 }
		{
			#send !hands;
		};

		#NOP { Toggle between tank and dps mode };
		#if { $spNormalized < 20 && $monk[dps] == 1 }
		{
			.dpsOff;
		};

		#if { $spNormalized > 95 && $monk[dps] == 0 }
		{
			.dpsOn;
		};

		#NOP { Auto cast hide of the dragon to save sp drain by spreading it out };
		#if { $patroller[isPatrolling] }
		{
			#if { $spNormalized < 90 && $monk[ae][current] >= 3 && "$monk[hide]" == "OFF" }
			{
				#math shieldRemaining {5-$monk[ae][reset]/20};
				#math currentShields { $monk[ae][current]/2};
				#if { $currentShields > $shieldRemaining }
				{
					#draw Yellow scroll line 1 1 1 80;
					#showme Preventing SP Drain;
					#draw Yellow scroll line 1 1 1 80;
					#send !hide of the dragon;
				};

			};
		}; 
		#elseif { $monk[ae][current] >= 3 && "$monk[hide]" == "OFF" && $monk[ae][reset] >= 82 }
		{
			#send !hide of the dragon;
			#var monk[hide] {*ACTIVE*};
		};

		#NOP { Auto cast palm };
		#if { !$monk[palm] && $monk[ae][current] > 0}
		{
			#send !palm;
		};

		#NOP { Auto cast pressure };
		#if { !$monk[pressure] }
		{
			#send !pressure;
		};

		#NOP { Auto cast hotd };
		#if { $hpNormalized < 80 && $monk[hide] != "*ACTIVE*" }
		{
			#send !hide of the dragon;
			#var monk[hide] {*ACTIVE*};
		};
	};
};

/*************************************************************************************
	Parse MIP
*************************************************************************************/
#ALIAS {onNewMipGlineMonk} 
{
	#replace {mip[gline][1]} {*} {};
	
	#REGEX {$mip[gline][1]} {<868>Method<099>: <838>%w<099>{?:\s+}<868>Crits<099>: <{?:\d+}>%w<099>{?:\s+}<868>Stun<099>: <{?:\d+}>%w<099>{?:\s+}<868>Hide<099>: <{?:\d+}>%w<099>} 
	{
		#var	{monk[method]}		{&1};
		#var	{monk[crits]}		{&2};
		#var	{monk[stun]}		{&3};
		#var	{monk[hide]}		{&4}
	};
	

	#REGEX {$mip[gline][2]} {G2N: <838>%d<099>{?:\s+}<868>Chi Focus<099>: <{?:\d+}>%w<099>{?:\s+}<838>AE<099>: <{\d+}>%d<099>/%d%%} 
	{
		#var	{monk[g2n][prev]}	{$monk[g2n][current]};
		#var	{monk[g2n][current]}	{&1};
		#var	{monk[chiFocus]}	{&2};
		#var	{monk[ae][current]}	{&4};
		#var	{monk[ae][reset]}	{&5};
		#math	{monk[gxp][last]}	{$monk[g2n][prev] - $monk[g2n][current]};

	};
};

/*************************************************************************************
	Render hp bar
*************************************************************************************/
#alias {.renderMonkBar} 
{
	#if {"$cooler" == "on" || $corpses[cooler] > 0} 
	{
		#format {sb1_cooler} {%+2s} {$corpses[cooler]};
		#var {sb1_cooler} { CL:<130>$sb1_cooler<088>};
	};
	#else
	{
			#var {sb1_cooler} {};
	};
	#if {"$freezer" == "on" || $corpses[freezer] > 0} 
	{
			#format {sb2_freezer} {%+2s} {$corpses[freezer]};
			#var {sb2_freezer} { FZ:<130>$sb2_freezer<088>};
	};
	#else
	{
			#var {sb2_freezer} {};
	};

	#var    {my[hp][graph]}         {@graph{$my[hp][current];${my[hp][max]}}};
	#var    {my[sp][graph]}         {@graph{$my[sp][current];$my[sp][max]}};
	#var    {my[gp1][graph]}        {@graph{$my[gp1][current];$my[gp1][max]}};
	#var    {my[gp2][graph]}        {@graph{$my[gp2][current];$my[gp2][max]}};
	#var    {enemy[graph]}          {@graph{$enemy[hp];100}};
	#format {hp}					{%-4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}				{%-4s} {<128>$my[hp][max]<088>};
	#format {sp}					{%-4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_max}				{%-4s} {<128>$my[sp][max]<088>};
	#format {gp1}                   {%-4s} {@colscale{$my[gp1][current];$my[gp1][max]}};
	#format {gp1_max}               {%+4s} {<128>$my[gp1][max]<088>};
	#format {gp2}                   {%-4s} {@colscale{$my[gp2][current];$my[gp2][max]}};
	#format {gp2_max}               {%+4s} {<128>$my[gp2][max]<088>};
	
	#if { "$enemy[name]" != "" }
	{
		#var dmgReport @getDamageReport{};
		#showme {<148>$enemy[name]<088> <048>(<138>$event[rating]<048>)<088> <048>[<138>$mip[round]<048>]<088> $enemy[graph]<088> $dmgReport} {-6};
	};
	#else
	{
		#showme {  } {-6};
	};
	#showme {<048>HP<088>  ${hp}/${hp_max}  <048>SP<088>  ${sp}<048>/<088>${sp_max}  <048>Chi<088>  ${gp1}<048>/<088>${gp1_max}  <048>Peace<088>  ${gp2}<048>/<088>${gp2_max} <048>AE<088> <128>$monk[ae][current]<088> <048>[<088><128>$monk[ae][reset]%<088><048>]<088>} {-3};
	#showme {$mip[gline][1]  $mip[gline][2]} {-4};
};

/*************************************************************************************
	Ansify monks
*************************************************************************************/
#highlight {You quickly tumble to avoid the brunt of the attack.} {<048>}
#highlight {With skill and speed you dodge the attack!} {<048>}
#highlight {You steadily block the brunt of the incoming attack.} {<048>};

#highlight {Your body is wracked in pain as the life force is sucked from you.} {<068>}
#highlight {You take an instant to gather your thoughts, clear your mind and} {<068>}
#highlight {focus on the task at hand.} {<068>}

#highlight {%1 dodges to one side avoiding your kick!} {<068>};


#highlight {You lay hands upon yourself and feel healed.} {<128>};
#highlight {You focus your mind and body and slowly tense your muscles in} {<128>}
#highlight {preparation to deliver a stunning mawashi-geri...} {<128>}
#highlight {You maneuver yourself into position to execute a Mawashi-Geri...} {<128>}

#highlight {Your great speed allows you to attack again!} {<028>}
#highlight {You find a pressure point and bore down!} {<028>}

#highlight {You reach out and touch your foe, sending death bringing vibrations through it.} {<018>}
#highlight {%1's body ruptures at all major joints!} {<018>}
#highlight {%1 screams in pain as they become weaker!} {<018>}
#highlight {With a sudden burst of speed you leap into the air and land a devastating} {<018>}
#highlight {roundhouse strike against %1!} {<018>}
#highlight {%1's body is violently rearranged!!} {<018>}

#HIGHLIGHT {%1's body contorts into a Picasso-like nightmare!} {<008>};
#highlight {You reach out and touch your foe, sending death bringing vibrations} {<018>}
#highlight {^through it.$} {<018>}
#highlight {^through him.$} {<018>}
#highlight {^through her.$} {<018>}


#gag {Your strike has weakened %1's defenses!};
#gag {With furious speed you strike again!};
#gag {You have become grounded in this plane yet again.}
#gag {You quickly tumble to avoid the brunt of the attack.}
#gag {Your muscles ripple and bulge with power.}
#gag {Electricity crackles through your fingers.}
#gag {With skill and speed you dodge the attack!};
#gag {Your great speed allows you to attack again!};

#gag {Psionic waves emanate from your body.};

#gag {A flash of insidious insight strikes you!};
#gag {You enter a state of unarmed frenzy!};
#gag {Your aura sparkles with blue lights!};
#gag {Your amulet flashes, and you phase out of harms way!};
#gag {A bolt of pure energy flies out of the sky striking %w};
#gag {You bring your deadly natural weapons down on your opponent!};
#gag {Bonehead, you're at max hit points already.};
#gag {Performing: hands};
#gag {Your body explodes in a *BLUR* of force and power!};



#highlight {You relax your mind and body and concentrate on the ancient powers of} {<048>};
#highlight {defense.  Energy swirls within you and the knowledge of countless} {<048>};
#highlight {generations pours into you.  Channeling your energy you feel your skin} {<048>};
#highlight {become hardened like the Scales of a Dragon.} {<048>};



#highlight {You begin to stretch and relax, concentrating deeply on muscle tone} {<048>};
#highlight {and muscle strength.  You practice your quickness and your strength} {<048>};
#highlight {and settle in on the fighting method of the %1.} {<048>};


#gag {Your unarmed frenzy ends.};
#gag {You find a pressure point and bore down!};
#gag {Your body is wracked in pain as the life force is sucked from you};

#gag {^You take an instant to gather your thoughts, clear your mind and$};
#gag {^focus on the task at hand.$}
 
 


#class {monk} {close}
