#class {angel} {open}

/* Set Guild Variable*/
#var guild Angel
#var angel[isEnabled] 1;
#var angel[distanceBetween] 0;
#var angel[distanceField] {};
#var angel[lastAction] {};
#var angel[threshold] 50;
#var angel[smack] 25;
.registerEvent onPreKilledMob onMobKilledAngel;
.registerEvent onMobAttacked onMobAttackedAngel;
.registerEvent onNewCombatRound onNewCombatRoundAngel;
.registerEvent onNewMipGline onNewMipGlineAngel;

/*************************************************************************************
	Mob was attacked
*************************************************************************************/
#alias {onMobAttackedAngel}
{
	#NOP {nothing};
};

/*************************************************************************************
	Mob was killed
*************************************************************************************/
#alias onMobKilledAngel
{
	#var angel[ds][ho]		0;
	#var angel[ds][hi]		0;
	#var angel[ds][lo]		0;
	#var angel[ds][li]		0;
	#var angel[ds][max]		0;
	#var angel[ds][type]	{--};
};

/*************************************************************************************
	Round of combat
*************************************************************************************/
#alias onNewCombatRoundAngel
{
	#if { $angel[isEnabled] }
	{
		#if { $angel[distanceBetween] > 1 }
		{
			#send !engage;
		};
		#else
		{
			#if { $angel[ds][ho] > $angel[smack] && $angel[ds][hi] > $angel[smack] && $angel[ds][lo] > $angel[smack] && $angel[ds][li] > $angel[smack] }
			{
				#send !perform KratSmack;
			};
			#else
			{
				#if { $angel[ds][max] > $angel[threshold] }
				{
					#send !divine strike $angel[ds][type];
				};
			};
		};
	};
};

/*************************************************************************************
	Mip sent us newest data

	hpset hpbar $FLAGS$HP:$HP$/$HPMAX$ | SP:$SP$/$SPMAX$ | TS:$TS$/$TSMAX$ | DV:$DV$/$DVMAX$ | EN:$EN$/$ENMAX$ $NL$ HO:$ADVHO$/$WKHO$ HI:$ADVHI$/$WKHI$ LO:$ADVLO$/$WKLO$ LI:$ADVLI$/$WKLI$ $NL$ Enemy:$ENEMY$ $DIST$
	hpset gline2 [$FLAGS$]$DIST$ F:$FOCUSSTAT$|$FOCUSGXP$ | TS:$TS$/$TSMAX$ | DV:$DV$/$DVMAX$ | EN:$EN$/$ENMAX$

*************************************************************************************/
#alias onNewMipGlineAngel
{
	#REGEXP {$mip[gline][1]} {HO:%d.%d/%d HI:%d.%d/%d LO:%d.%d/%d LI:%d.%d/%d} 
	{
		#var angel[ds][ho]		{&1.&2};
		#var angel[ds][hi]		{&4.&5};
		#var angel[ds][lo]		{&7.&8};
		#var angel[ds][li]		{&10.&11};
		#var angel[ds][max]		{$angel[ds][ho]};
		#var angel[ds][type]	{ho};
		
		#if {$angel[ds][hi] > $angel[ds][max]} 
		{
			#var angel[ds][max]	{$angel[ds][hi]};
			#var angel[ds][type] {hi};
		};
	
		#if {$angel[ds][lo] > $angel[ds][max]} 
		{
			#var angel[ds][max]	{$angel[ds][lo]};
			#var angel[ds][type] {lo};
		};
	
		#if {$angel[ds][li] > $angel[ds][max]} 
		{
			#var angel[ds][max]	{$angel[ds][li]};
			#var angel[ds][type] {li};
		};
	} {
		#NOP no response;
	};

	#REGEXP {$mip[gline][2]} {F:%w|%d.%d\% | TS:%d/%d | DV:%d/%d | EN:%d/%d}
	{
		#var angel[focusSkill] {&1};
		#var angel[focus] {&2.&3};
		#var angel[timeSlice] &4;
		#var angel[timeSliceMax] &5;
		#var angel[divinity] &6;
		#var angel[divinityMax] &7;
		#var angel[endurance] &8;
		#var angel[enduranceMax] &9;		
	};

	#REGEXP {$mip[gline][2]} {^{\[\](.*)(F.*)}$}
	{
		#var {angel[distanceField]} {&2};
	} {
		#NOP no response;
	};

	#REGEXP {$mip[gline][2]} {^{|.*[PM](-*)[PM].*|}$} 
	{
		#var {dField} {&2};
	} {
		#NOP no response;
	};

	#if { "$dField" != "" }
	{ 
		#var angel[distanceBetween] @strlen{ $dField }; 
	};	
};

/*************************************************************************************
	Renders custom HP Bar for this guild
*************************************************************************************/
#alias {.renderAngelBar}
{
	#if {"$cooler" == "on" || $corpses[cooler] > 0} 
	{
			#format {sb1_cooler} {%+2s} {$corpses[cooler]};
			#var {sb1_cooler} { CL:<130>$sb1_cooler<088>}
	};
	#else
	{
			#var {sb1_cooler} {}
	};

	#if {"$freezer" == "on" || $corpses[freezer] > 0} 
	{
			#format {sb2_freezer} {%+2s} {$corpses[freezer]};
			#var {sb2_freezer} { FZ:<130>$sb2_freezer<088>}
	};
	#ELSE 
	{
			#var {sb2_freezer} {}
	};

	#var {my[hp][graph]}		{@graph{$my[hp][current];$my[hp][max]}};
	#var {my[sp][graph]}		{@graph{$my[sp][current];$my[sp][max]}};
	#var {my[gp1][graph]}		{@graph{$my[gp1][current];$my[gp1][max]}};
	#var {my[gp2][graph]}		{@graph{$my[gp2][current];$my[gp2][max]}};
	#var {angel[g][graph]}		{@graph{$angel[g][per];100}};
	#var {enemy[graph]}		{@graph{$enemy[hp];100}};
	#format {hp}			{%-4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}		{%-4s} {<128>$my[hp][max]<088>};
	#format {sp}			{%-4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_max}		{%-4s} {<128>$my[sp][max]<088>};
	#format {time}			{%+4s} {@colscale{$my[gp1][current];$my[gp1][max]}};
	#format {time_max}		{%-4s} {<128>$my[gp1][max]<088>};
	#format {divinity}		{%+4s} {@colscale{$my[gp2][current];$my[gp2][max]}};
	#format {divinity_max}	{%-4s} {<128>$my[gp2][max]<088>};
	#format {endurance}		{%+4s} {@colscale{$angel[endurance];$angel[enduranceMax]}};
	#format {endurance_max}	{%-4s} {<128$angel[enduranceMax]<088>};


	#format {ehealth}		{%+3s} {@colscale{$enemy[hp];100}};
	#format {corpses[inventory]}    {%+2s} {$corpses[inventory]};
	#format {corpses[coffin]}       {%+2s} {$corpses[coffin]};

	#showme {<048>H<088> ${hp}/${hp_max} <048>TS<088> ${time}/${time_max} <048>D<088> ${divinity}/${divinity_max} <048>E<088> ${endurance}} {-3};

	#if { "$enemy[name]" != "" }
	{
		#showme {<038>HO: <138>$angel[ds][ho]<088> <038>HI: <138>$angel[ds][hi]<088> <038>LO: <138>$angel[ds][lo]<088> <038>LI: <138>$angel[ds][li]<088> <048>$angel[distanceField]<088> <148>$angel[distanceBetween]<088>} {-4};
		#showme {<148>$enemy[name]<088> <048>(<138>$dmg[rating]<048>)<088> <048>[<138>$mip[round]<048>]<088> $enemy[graph]<088>} {-6};
	};
	#else
	{
		#showme {  } {-6};
		#showme {<048>$angel[focusSkill]<088> <138>$angel[focus]<088> <048>Corpses:  I <138>$corpses[inventory]<088> <048>C <138>$corpses[coffin]<088>} {-4};
	};

};

#ACTION {You are already as close as you can get to your foe!} 
{
	#var angel[distanceBetween] 0;
} {5};

#class {angel} {close}
