#showme READING MAPPER
#read 3k/TinTin/Mapper/Localizer.tin

#class {mapper} {open}

/*************************************************************
	Map Data
*************************************************************/
#map read 3k/TinTin/Data/3k.map
#var longcap off;
#var finding_here off;
#var mapstatic 1;
#var area ????;
#var mapping off;
#var room_cnt 0;
#var currentRoomID 0;
#map flag static on;
#map flag vtmap on;
/*************************************************************
	Events
*************************************************************/
.registerEvent onFollowerFollowed onFollowerFollowedMapResponder;
.registerEvent onPostWimpy onWimpyMapResponder;
.registerEvent onFailedMovement onFailedMovementMapResponder;
.registerEvent onBlockedMovement onBlockedMovementMapResponder;

#event {CLASS DESTROYED mapper}
{
	.unregisterEvent onFollowerFollowed onFollowerFollowedMapResponder;
	.unregisterEvent onPostWimpy onWimpyMapResponder;
	.unregisterEvent onFailedMovement onFailedMovementMapResponder;
	.unregisterEvent onBlockedMovement onBlockedMovementMapResponder;
}

/**************************************************************
	Sets an exit to the current room
**************************************************************/
#alias .mapSameRoom
{
        #map link %0 $currentRoomID;
};

/**************************************************************
	Prints out how many unique 'areas' are in the map
**************************************************************/
#alias {.mapAreaCount}
{
	#draw Yellow scroll line 1 1 1 80;
	#var areaList {};
        #map list {roomname} {%*} {variable} {rooms}; 

	#foreach {*rooms[]} {roomID}  
	{
	        #map get all {roomdata} $roomID;
                #var areaList[$roomdata[area]] 1;
        };

        #showme Number of unique areas: @getNumElements{areaList};
        #draw Yellow scroll line 1 1 1 80;
};

/**************************************************************
	Remaps an exit to another direction .mapChangeExit n s
	Will make the north exit become the south exit
**************************************************************/
#alias .mapChangeExit
{
	#map get {roomvnum} {currentRoomID};
    #map at $currentRoomID #map get all {roomdata};
    #var exitID $roomdata[exits][%1];

	#if { "$exitID" != "" }
	{
			#map unlink %1;
			#map link %2 $exitID;
			#map at $exitID #map unlink @getOppositeDirection{%1};
			#map at $exitID #map link @getOppositeDirection{%2} $currentRoomID;
	};
};


/**************************************************************
	Lists rooms that haven't been explicitly marked explored
		Use: .describeUnexplored <areaname>
		Where areaname matches the rating command.
**************************************************************/
#alias {.mapSetExplored}
{
	#if { "%0" != "" }
	{
		#local areaToSearch %0;
	};
        #else
        {
                #showme "MUST SPECIFY AREA";
                #return;
        };
	
	#map list {roomarea} {^$areaToSearch$} {variable} {rooms}; 
	#draw Yellow scroll line 1 1 1 80;
	#var totalRooms @getNumElements{rooms};
	#var exploredRooms 0;
	
	#foreach {*rooms[]} {roomID}  
	{
		#map get all {roomdata} $roomID;
                #map at $roomID #map set roomid 1;

                #math {exploredRooms} {$exploredRooms+1};

                #var exits {};
                #foreach {*roomdata[exits][]} {exit}
                {
                        #var exits {$exits $exit};
                };


                #echo {<129>Room<088> [<129>%+5s<088>] %-20.20s <129>%+40.30s <268>%.30s<088> %.5s } {$roomID} {[<168>$roomdata[area]<088>]} {$roomdata[name]} {$exits} {$roomdata[symbol]}
	};
	
	#draw Yellow scroll line 1 1 1 80;
	#showme Left to explore [$exploredRooms] out of [$totalRooms];
};

/**************************************************************
	Outputs room data as html
**************************************************************/
#alias {.describeRoomsHtml}
{
	#local areaToSearch $area;
	#if { "%0" != "" }
	{
		#local areaToSearch %0;
	};

	#draw Yellow scroll line 1 1 1 80;

        #showme <table>;
	#showme AREATO : $areaToSearch;
	#map list {roomarea} {^$areaToSearch$} {variable} {rooms}; 
	#foreach {*rooms[]} {roomID}  
	{
		#map at $roomID #map get all {roomdata};

		#var exits {};
		#foreach {*roomdata[exits][]} {exit}
		{
			#var exits {$exits $exit};
		};

		#showme <tr><td>$roomdata[name]</td><td>$roomdata[area]</td><td>$exits</td><td>$roomdata[symbol]</td></tr>;
	};
	#showme </table>;
	#draw Yellow scroll line 1 1 1 80;
};

/**************************************************************
	Shows a list of rooms in an area that we have marked
	as unexplored by our client
**************************************************************/
#alias {.mapUnexploredRooms}
{
	#map list {roomname} {%*} {variable} {rooms}; 
	#draw Yellow scroll line 1 1 1 80;
	#var totalRooms @getNumElements{rooms};
	#var exploredRooms 0;
	
	#foreach {*rooms[]} {roomID}  
	{
		#map get all {roomdata} $roomID;
		
                #if { $roomID < %1 }
                {
                        #continue;
                };

                #if { "$roomdata[name]" == "" }
                {
                        #continue;
                };

		#if { $roomdata[id] == 1 }
		{
			#nop { DO NOTHING };
		};
		#else
		{
			#math {exploredRooms} {$exploredRooms+1};

			#var exits {};
			#foreach {*roomdata[exits][]} {exit}
			{
				#var exits {$exits $exit};
			};


			#echo {<129>Room<088> [<129>%+5s<088>] %-20.20s <129>%+40.30s <268>%.30s<088> %.5s } {$roomID} {[<168>$roomdata[area]<088>]} {$roomdata[name]} {$exits} {$roomdata[symbol]};

			#if { $exploredRooms > 50 }
			{
				#break;
			};
		};

	};
	
	#draw Yellow scroll line 1 1 1 80;
	#showme Left to explore [$exploredRooms] out of [$totalRooms];
};

/**************************************************************
	Lists rooms that haven't been explicitly marked explored
		Use: .describeUnexplored <areaname>
		Where areaname matches the rating command.
**************************************************************/
#alias {.mapUnexplored}
{
	#local areaToSearch $area;
	#if { "%0" != "" }
	{
		#local areaToSearch %0;
	};

	
	#map list {roomarea} {^$areaToSearch$} {variable} {rooms}; 
	#draw Yellow scroll line 1 1 1 80;
	#var totalRooms @getNumElements{rooms};
	#var exploredRooms 0;
	
	#foreach {*rooms[]} {roomID}  
	{
		#map get all {roomdata} $roomID;
		
		#if { $roomdata[id] == 1 }
		{
			#nop { DO NOTHING };
		};
		#else
		{
			#math {exploredRooms} {$exploredRooms+1};

			#var exits {};
			#foreach {*roomdata[exits][]} {exit}
			{
				#var exits {$exits $exit};
			};


			#echo {<129>Room<088> [<129>%+5s<088>] %-20.20s <129>%+40.30s <268>%.30s<088> %.5s } {$roomID} {[<168>$roomdata[area]<088>]} {$roomdata[name]} {$exits} {$roomdata[symbol]}
		};

	};
	
	#draw Yellow scroll line 1 1 1 80;
	#showme Left to explore [$exploredRooms] out of [$totalRooms];
};

/*************************************************************
 TinTin Events, redraw the map when the map data changes
*************************************************************/
#event {MAP ENTER ROOM} 
{
	#map get {roomvnum} {roomID};
	#map get {roomarea} {areaTmp};

	#if { "$areaTmp" != "" }
	{
		#var area $areaTmp;
	};

	#if { !@isDigit{$areaTmp} }
	{
		#var area $areaTmp;
	};

	#var event[enteredRoomID] $roomID;
	#var currentRoomID $roomID;
	#map set roomid 1;
	
	.triggerEvent onEnteredRoom;
}

/*************************************************************
 TinTin Events, player exited room
*************************************************************/
#event {MAP EXIT ROOM} 
{
	#map get {roomvnum} {roomID};
	#var event[leftRoomID] $roomID;
	#var lastRoomID $roomID;

	.triggerEvent onExitedRoom;
}

/*************************************************************
  Handling a response due to a wimpy flee
*************************************************************/
#alias onWimpyMapResponder
{
	#map move $event[wimpyDirection];
};

/*************************************************************
  Handling following someone
*************************************************************/
#alias onFollowerFollowed
{
	#map move $event[followDirection];
};

/*************************************************************
	Data grab trigger
*************************************************************/
#function cartcheck 
{
	#format cart {%.40s} {%0};
	#if {"$cart" == "                                        " || "$cart" == ""} 
	{
		#return 1
	};
	#else
	{
		#return 0
	};
}

/*************************************************************
	This action tries to find a room description combined 
	with exits
*************************************************************/
#act {{=S=(.*) \((.*)\)\s*=S=}} 
{
	#var room %2;
	#var exits %3;
	#if {"$mapping" == "on"} 
	{
		#if {"$finding_here" == "off"} 
		{
			#replace exits {,} {;};
			#foreach {$exits} {eachexit} 
			{
				#switch {"$eachexit"} 
				{
					#case {"north"}		{#map dig n};
					#case {"east"}		{#map dig e};
					#case {"south"}		{#map dig s};
					#case {"west"}		{#map dig w};
					#case {"northwest"}	{#map dig nw};
					#case {"northeast"}	{#map dig ne};
					#case {"southeast"}	{#map dig se};
					#case {"southwest"}	{#map dig sw};
					#case {"up"}		{#map dig u};
					#case {"down"}		{#map dig d};
					#default		{#map dig $eachexit}
				}
			};
			#map get {roomexit} {rexit};
			#foreach {$rexit} {eachexit} 
			{
				#map at {$eachexit} 
				{
					#map get {roomarea} {eacharea};
					#if {"$eacharea" == ""} 
					{
						#map set {roomcolor} {<118>}
					}
				}
			}
		};
		#if {$roomname_len == 0} 
		{
			#map set roomname {%2 (%3)}
		};
		#if {$roomarea_len == 0} 
		{
			#map set roomarea {$area};
			#map set roomcolor
		};
		#if {$roomdesc_len == 0 || "$finding_here" == "on"} 
		{
			#var longcap on;
			#var long {};
			#act {%%1} 
			{
				#if {@cartcheck{%%1}} 
				{
					#nop
				};
				#else
				{
					#var long { ${long}%%1 };
				}
			} {2}
		};
		#delay 0 .renderMapInfo
	}

	.triggerEvent onRoomSeen;

} {2};

/*************************************************************
	Expression for finding the room description, no exits
*************************************************************/
#act {=S=%1%s=S=} 
{
	#var room %1;

	#if { "$mapping" == "on" } 
	{
		#if { $roomname_len == 0 }
		{
			#map set roomname {%1}
		};

		#if {$roomarea_len == 0} 
		{
			#map set roomarea {$area};
			#map set roomcolor
		};

		#if { $roomdesc_len == 0 || "$finding_here" == "on" } 
		{
			#var longcap on;
			#unvar long;
			#act {%%1} 
			{
				#if {@cartcheck{%%1}} 
				{
					#nop
				};
				#else
				{
					#var long { ${long}%%1 }
				}
			} {2}
		};

		#map get {roomvnum} {gonum};
		#map goto $gonum;
	}
} {6}

/*************************************************************
This action is to find the room exits after a long description
Once we see this, we can conclude the room description is over
*************************************************************/
#act {=X={.*}} 
{
	#if {"$longcap" == "on"} 
	{
		#var longcap off;
		#unact {%%1};
		#if {"$finding_here" == "off"} 
		{
			#map set roomdesc {$long}
		}
	}
} {1};

/*************************************************************
The area has changed 
*************************************************************/
#act {AREA NAME: %1 [%2]} 
{
	#var area %1;
	#var wizardName %2;
}

/*************************************************************
   Turns the portion of map in a direction into a new subarea
   Ie:  .mapSubArea w
*************************************************************/
#alias .mapSubArea
{
        #map exit %1 dir %1;
        #map exit %1 flags 1;
};

/*************************************************************
    Map last room with direction special exit
	example: .mlr e out    connects the last room we where
	in to 'east' with a custom 'out' command
*************************************************************/
#alias {.mlr}
{
 .mapLastRoom %1;
 .mapLastRoom %2;
 .mapCommand %1 %2;
};

/*************************************************************
 Responding to blocked exits in general. 
*************************************************************/
#alias onFailedMovementMapResponder
{
	#if { !$mapstatic }  
	{
		#map undo;
	};
};

/*************************************************************
 Responding to blocked exits in general. 
*************************************************************/
#alias onBlockedMovementMapResponder
{
	#map undo;
};

/*************************************************************
	Helper Calls - Mapped Area Counter
*************************************************************/
#alias {area_mapped %1} 
{
	#unvar area_mapped;
	#map list {} {} {} {%1} {variable} {area_mapped};
	#showme {&area_mapped[] rooms mapped.};
};


#alias .describeNote
{
	#map at {{roomnote}{%1}};
	#map get roomvnum bla;
	#showme vnum: $bla;
}

/*************************************************************
	Map actions
*************************************************************/
#alias {.go} 
{
	#var traveller[isTravelling] 1;
	#UNDELAY {travelFinished};
	#DELAY {travelFinished} { #var traveller[isTravelling] 0; } {4};
	
	#if { "%1" == "%d" }
	{
		#map run %1;
	};
	#else
	{
		#map run {{roomnote} {%1}};
	};
}

/**************************************************************
        Sets the map to a specific landmark
**************************************************************/
#alias {.mapGo} {#map goto {roomnote} {%0}}

/**************************************************************
        See if we can locate ourselves on the map
**************************************************************/
#alias {findme} 
{
        #map go $room ($exits);
};

/*****************************************************************************
	Finds here
*****************************************************************************/
#alias {findhere} 
{
	#var finding_here on;
	rating;
	#delay 0 mapon;
	#delay 0.1 look;
	#delay 0.2
	{
		#var findexits $exits;
		#replace findexits {,} {;};
		#map list {roomarea} {$area} {roomname} {$room%*} {roomexits} {$findexits} {roomdesc} {$long};
		#map list {roomarea} {$area} {roomname} {$room%*} {roomexits} {$findexits} {roomdesc} {$long} {variable} {findroom};
		#if {&findroom[] == 1} 
		{
			#map goto *findroom[1..1]
		};
		#var finding_here off;
		.mapOff
	}
}


/*****************************************************************************
	Sends a path in tell 
*****************************************************************************/
#alias .mapTellPath
{
        #map at {{roomnote}{%1}} #map get roomvnum roomid;
        #map find $roomid;
        #path save forward pathToWalk;

        #map get roomname {currentName};
        #map get roomarea {currentArea};
        #map at {{roomnote}{%1}} #map get roomname {destName};
        #map at {{roomnote}{%1}} #map get roomarea {destArea};

        #replace {pathToWalk} {\;} {,};
        #send !tell %2 Showing directions from $currentName in $currentArea to $destName in $destArea;
        #send !tell %2 $pathToWalk;
};


/*****************************************************************************
	Says path to room src to dst
*****************************************************************************/
#alias .mapSayPath
{
        #map at {{roomnote}{%0}} #map get roomvnum roomid;
        #map find $roomid;
        #path save forward pathToWalk;

        #map get roomname {currentName};
        #map get roomarea {currentArea};
        #map at {{roomnote}{%1}} #map get roomname {destName};
        #map at {{roomnote}{%1}} #map get roomarea {destArea};

        #replace {pathToWalk} {\;} {,};
        #send say %2 Showing directions from $currentName in $currentArea to $destName in $destArea;
        #send say %2 $pathToWalk;
};


/*****************************************************************************
	Shows the path from src to dst
*****************************************************************************/
#alias .mapShowPath
{
        #var savedID $currentRoomID;
        #var destination %1;
        #var pathToWalk {};

        #if { "%2" != "" }
        {
                .mapGo %1;
                #var destination %2;
        };

        #map at {{roomnote}{$destination}} #map get roomvnum roomid;
        #map find $roomid;
        #path save forward pathToWalk;

        #map get roomname {currentName};
        #map get roomarea {currentArea};
        #map at {{roomnote}{$destination}} #map get roomname {destName};
        #map at {{roomnote}{$destination}} #map get roomarea {destArea};

        #draw Yellow scroll line 1 1 1 80;
        #showme Directions from $currentName in $currentArea to $destName in $destArea;
        #draw Yellow scroll line 1 1 1 80;
        #showme $pathToWalk;

        #if { "%2" != "" }
        {
                #map go $savedID;
        };
};





#alias {.describeRoom}
{
	#map {get} {ROOMVNUM} {original};
	#map go %1;

	#map {get} {ROOMNAME} {roomname};
	#map {get} {ROOMVNUM} {roomvnum};
	#map {get} {ROOMAREA} {roomarea};
	#map {get} {ROOMDESC} {roomdesc};
	
	#showme {Describing: [<139>$roomvnum<088>] - [<039>$roomname<088>] in [<039>$roomarea<088>]};
	#draw Yellow scroll line 1 1 1 80;
	#showme $roomdesc;
	#draw Yellow scroll line 1 1 1 80;

	#map go $original;
}


/*****************************************************************************
	Creates a link to the last room with a given name
*****************************************************************************/
#alias .mapLastRoom 
{ 
	#if { "%2" != "" }
	{
		#map link {%1} $lastRoomID;
		#map exit {%1} command {%2};
	};
	#else
	{
		#map link {%1} $lastRoomID;
	};
};

/*****************************************************************************
	Links a custom command to a direciton
		exmapl: .mapCommand n flee
*****************************************************************************/
#alias .mapCommand 
{
        #if { @containsNoCase{ {%2} {\;} } }
        {
                #map at $currentRoomID #map get all {roomdata};
                #var exitID $roomdata[exits][%1];

                #if { "$exitID" == "" }
                {
                        #map dig {%1};
                };

                #map exit {%1} command {%2};	
        };
        #else
        {
                #map {get} {ROOMVNUM} {currentRoomID};
                #map at $currentRoomID #map get all {roomdata};

                #var exitID $roomdata[exits][%2];

                #if { "$exitID" != "" }
                {
                        #map link {%1} $exitID;
                        #map exit {%1} command {%2};
                };
                #else
                {
                        #map dig {%2};
                        #map at $currentRoomID #map get all {roomdata};
                        #var exitID $roomdata[exits][%2];

                        #map link {%1} $exitID;
                        #map exit {%1} command {%2};

                };
        };
};

/*****************************************************************************
	Search map for rooms that have a substring in their long descriptions
*****************************************************************************/
#alias {.searchAreaForDesc}
{	
	#draw Yellow scroll line 1 1 1 80;
	#map list {roomdesc} {%*%0%*} {roomarea} {$area} {variable} {rooms}; 
	#foreach {*rooms[]} {roomID}  
	{ 
		#map at $roomID #map get all {roomdata};
		#local exits {};

		#foreach {*roomdata[exits][]} {exit} 
		{
			#if { "$exit" != "" }
			{
				#if { "$exits" == "" }
				{
					#local exits {$exit};
				};
				#ELSE 
				{
					#local exits {$exits,$exit};
				};
				
			};
		};


		#echo {<129>Room<088> [<129>%+5s<088>] %-20.20s <129>%+40.30s <268>Exits %.30s<088> } {$roomID} {[<168>$roomdata[area]<088>]} {$roomdata[name]} {$exits}
	};
	
	#draw Yellow scroll line 1 1 1 80;
};


/*****************************************************************************
	Search map for rooms that have a substring in their long descriptions
*****************************************************************************/
#alias {.searchMapForDesc}
{	
	#draw Yellow scroll line 1 1 1 80;
	#map list {roomdesc} {%*%0%*} {variable} {rooms}; 
	#foreach {*rooms[]} {roomID}  
	{ 
		#map at $roomID #map get all {roomdata};
		#local exits {};

		#foreach {*roomdata[exits][]} {exit} 
		{
			#if { "$exit" != "" }
			{
				#if { "$exits" == "" }
				{
					#local exits {$exit};
				};
				#ELSE 
				{
					#local exits {$exits,$exit};
				};
				
			};
		};


		#echo {<129>Room<088> [<129>%+5s<088>] %-20.20s <129>%+40.30s <268>Exits %.30s<088> } {$roomID} {[<168>$roomdata[area]<088>]} {$roomdata[name]} {$exits}
	};
	#draw Yellow scroll line 1 1 1 80;

};

/*****************************************************************************
	Search map for rooms that have a substring in their name
*****************************************************************************/
#alias {.searchMapForRoom} 
{
	#show <129>Name matches:<088>;
	#map list {%i%*%0%*};
	#show \n;
	#show <129>Note matches:<088>;
	#map list {} {} {} {} {%i%*%0%*};
};

/*****************************************************************************
	Saves the map
*****************************************************************************/
#alias {.mapSave}
{
	#map write 3k/TinTin/Data/3k.map;
};

/*****************************************************************************
	When you hit the no obvious exit cases, it breaks the parser
*****************************************************************************/
#alias {.mapSetRoomName}
{
	#map set roomarea $area;
	#map set roomname %1;
}

#alias {.mapSetArea} 
{
	#map set roomarea {$area};
};

#alias {.mapAnchor} 
{
	#map get {roomvnum} {mapAnchor};
};

#alias {.mapReturn}
{
	#var traveller[isTravelling] 1;
	#UNDELAY {travelFinished};
	#DELAY {travelFinished} { #var traveller[isTravelling] 0; } {4};

	#map run $mapAnchor;
};

#alias {.mapOn} 
{
	#send !brief off yes;
	#map flag static off;
	#var mapstatic 0;
	#var area %1;
	#var mapping on;
};

#alias {.mapOff}
{
	#send !brief on yes;
	#map flag static on;
	#var mapstatic 1;	
	#unvar mapping;
};



#NOP List all Roomnotes for 'go' alias
#alias {.noteList} 
{
	#map list {roomnote} {variable} {noterooms};
	#foreach {*noterooms[]} {room} {#map get {roomnote} {notes[$room]} {$room}};
	#list notes sort;
	#var notes
};

#NOP Link rooms with a void insert
#alias voidlink 
{
	#var dir %1;
	#var dest_num %2;
	#map link $dir $dest_num both;
	#map insert $dir void
};

/* Highmortal Portals Toggles */
#alias hmavoid 
{
	#map at 148 {#map exitflag {portal road} avoid on};
	#map at 148 {#map exitflag {portal eforest} avoid on};
	#map at 148 {#map exitflag {portal wforest} avoid on};
	#map at 148 {#map exitflag {efor} avoid on};
	#map at 148 {#map exitflag {portal desert} avoid on};
	#map at 148 {#map exitflag {portal ice} avoid on}
};

#alias hmfollow 
{
	#map at 148 {#map exitflag {portal road} avoid off};
	#map at 148 {#map exitflag {portal eforest} avoid off};
	#map at 148 {#map exitflag {portal wforest} avoid off};
	#map at 148 {#map exitflag {efor} avoid off};
	#map at 148 {#map exitflag {portal desert} avoid off};
	#map at 148 {#map exitflag {portal ice} avoid off}
};

/* House avoid for followers */
#NOP Using high weight instead of avoid so paths into house will still work, but pathing will avoid it going through town
#alias homeavoid 
{
	#map at 152 {#map set roomweight 100};
};

#alias homefollow 
{
	#map at 152 {#map set roomweight 1};
};

#class {mapper} {close}
