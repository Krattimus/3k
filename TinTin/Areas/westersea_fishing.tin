#class {bot} {open}

#draw Yellow scroll line 1 1 1 80;
#showme Fishing Level is set to: $fish[level]. Set var fish[level] to change it.
#draw Yellow scroll line 1 1 1 80;

#var fish[pool] $fish[level];
#var fish[targetScore] {40};
#var fish[maxTension] {35};
#var fish[manual] {};
#var fish[better] 0;
/******************************************************************************
	Data to be used for this patrol
*******************************************************************************/
#var patrol[startingLocation] 6211;
#var patrol[area] {The City of Westersea};
#var patrol[path] {fish $fish[pool]; fish}
 
/******************************************************************************
	What to attack
*******************************************************************************/
#var patrol[shouldKillList] 
{
};

/******************************************************************************
	Additional scripting used for this area

			...

*******************************************************************************/

#act {^Suddenly something grabs hole of your line!!!} {reel}
#act {^Even though you do everything right the catch manages to wriggle free} 
{
	.logChat {<108>[Fishing]<088> Fish Lost ($fish[catch])};
	#var fish[better] 0;
	fish
};

#act {^You fail to retrieve your catch!} 
{
	.logChat {<108>[Fishing]<088> Fish Lost ($fish[catch])};
	#var fish[better] 0;
	fish;
};

#act {^You successfully manage to retrieve your catch!} 
{
	.logChat {<108>[Fishing]<088> <128>Fish Caught<088> ($fish[catch])};
	#var fish[better] 0;
	#send !take all; 
	#send !fish;
};

#act {^You're too tired to do that.} 
{
	#var fish[better] 0;
	catch;
	fish;
};

#act {^As no fish appear to be interested in your line, you pull it back up.} {bait rod;fish}
#act {^You notice that your bait is gone!} {bait rod;fish}
#act {^You can't fish without a line!} 
{
	#var fish[better] 0;
	thread rod;
	bait rod;
	fish
};


#act {^Your fishing rod bends violently and cracks in two!} 
{
	.logChat {<108>[Fishing] <118>Rod broke<088> ($fish[catch])};
	#var fish[better] 0;
	thread rod;
	bait rod;
	fish;
};

#act {^You hear a loud snap as your line breaks!} 
{
	.logChat {<108>[Fishing] <118>Line broke<088> ($fish[catch])};
	#var fish[better] 0;
	thread rod;
	bait rod;
	fish;
};

#act {^With a loud 'thwang', your line snaps. Perhaps you should pay more} 
{
	.logChat {<108>[Fishing] <118>Line broke<088> ($fish[catch])};
	#var fish[better] 0;
	thread rod;
	bait rod;
	fish;
};

#act {^Your fishing rod bends violenty and cracks in two!} 
{
	#draw Yellow scroll line 1 1 1 80;
	#showme GO BUY A ROD!;
	#draw Yellow scroll line 1 1 1 80;

	#send !return;
	#class bot kill;
};

#act {^You have nothing to bait the rod with!}
 {
	#draw Yellow scroll line 1 1 1 80;
	#showme GO BUY BAIT!;
	#draw Yellow scroll line 1 1 1 80;

	#send !return;
	#class bot kill;
}



#act {^You have nothing to thread the rod with!} 
{
	#draw Yellow scroll line 1 1 1 80;
	#showme GO BUY THREAD!;
	#draw Yellow scroll line 1 1 1 80;

	#send !return;
	#class bot kill;
};

#alias .reel { #var fish[manual] {reel}; };
#alias .pullRight { #var fish[manual] {pull right}; };
#alias .pullLeft { #var fish[manual] {pull left}; };
#alias .slack { #var fish[manual] {slack}; };
#alias .better { #var fish[better] 1; };


#act {{End:(\d+) E Req:(\d+|[LHM]) Dis:(\d+|[LHM])(?:cm)? Dir:([LRC+]+) Ten:(\d+|[LMH]) Line:(\d+|[LMH])+\%? Catch:(\d+|[LMH])+\%?}} 
{
	#var fish[end]		%2;
	#var fish[req]		%3;
	#var fish[dist]		%4;
	#var fish[dir]		%5;
	#var fish[ten]		%6;
	#var fish[line]		%7;
	#var fish[catch]	%8;

	#switch {"$fish[req]"} 
	{
		#case {"L"}	{#var fish[req] 40};
		#case {"M"}	{#var fish[req] 60};
		#case {"H"}	{#var fish[req] 80};
		#default 	{#nop};
	};

	#if {"$fish[dist]" == "H"} { #var fish[dist] 200; };

	#if { "$fish[manual]" != "" }
	{
		#draw Yellow scroll line 1 1 1 80;
		#showme $fish[manual];
		#draw Yellow scroll line 1 1 1 80;
		#send !$fish[manual];
		#var fish[manual] {};
	};
	#else
	{
		#if { $fish[end] <= ($fish[req]+40)} { #send !catch;};
		#elseif {$fish[catch] > $fish[targetScore] && !$fish[better]} { #send !catch; };
		#elseif {$fish[ten] > $fish[maxTension] || "$fish[ten]" == "H" } { #send !slack;};
		#elseif {"$fish[dir]" == "R++"} {#send !pull left;};
		#elseif {"$fish[dir]" == "L++"} {#send !pull right;};
		#elseif {$fish[dist] > 300} { #send !reel;};
		#elseif {"$fish[dir]" == "R+"} {#send !pull left;};
		#elseif {"$fish[dir]" == "L+"} {#send !pull right;};
		#elseif {$fish[dist] > 100} { #send !reel;};
		#elseif {$fish[ten] > 5 } { #send !slack;};
		#elseif {"$fish[dir]" == "R"} {#send !pull left;};
		#elseif {"$fish[dir]" == "L"} {#send !pull right;};
		#else {#send !reel;};
	};
};

#gag {^You can't do another action yet.}

#class {bot} {close}
