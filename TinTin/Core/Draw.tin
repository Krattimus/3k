#CLASS draw {open}

/* Load draw variables */
#read 3k/TinTin/Data/Draw.var

#var panelsOpen 0;

/************************************************************
	Determine Dimensions
************************************************************/
#var alert[width] 1
#var canvas[width] 43;
#var caption[height] 2
#math chat[height] {$chat[rows] + 1};
#math {chat[right]} {-$map[width]-2}
#var gline[height] 2
#var hpbar[height] 3

#math status[height] {$hpbar[height] + $gline[height] + 1}
#math split[top]	{$chat[height] + $caption[height]}
#var split[bottom]	$status[height]
#var split[left]	$alert[width]
#var split[right]	$canvas[width]

#draw {line} $chat[height]+$caption[height]+1 -$canvas[width] -$status[height]-2 -$canvas[width];
#showme {<228>\u252C<088>} $chat[height]+$caption[height] -$canvas[width];
#showme {<088>\u2534<088>} -$hpbar[height]-$gline[height]-2 -$canvas[width];

/************************************************************
	Split
************************************************************/
#split $split[top] $split[bottom] $split[left] $split[right]
#screen clear split

#draw {line} -2 1 -2 -1;
/************************************************************
	Render our displays
		foreach display in display ( render );
************************************************************/
.renderServerBar;
chatend;
.renderHPBar;


#if {"$draw[canvas]" == "on"} 
{

	#if { $miner[isMining] }
	{
		.renderMiningPanel;
	};
	#elseif { $inventoryOpen }
	{
		.renderInventory;
	};
	#else
	{
		.renderPatrols;
	};
}

.renderMiniMap;


#alias .togglePanel
{
	#if { "$draw[%1]" == "on" }
	{
		#var draw[%1] off;
	};
	#else
	{
		#var draw[%1] on;
	};

	#class drawvar write 3k/TinTin/Data/Draw.var;
	.render;
}

#CLASS draw {close}