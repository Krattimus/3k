#showme READING CORE.TIN

#class {aliases} {open}

#var stoppedController {none};
#var currentController {none};
#var resumeController {none};

#alias {.debugLog}
{
        .triggerEvent onDebugLog;
};

/*********************************************************
	Stops controllers
*********************************************************/
#alias {.stop}
{
        #undelay {doMiningStart};
        #undelay {doLoadPatrollStart};
        #undelay {doPatrolStart};

        #var stoppedController $currentController;
        .triggerEvent onRequestStop;
} {1};

/*********************************************************
	Calls resume on a controller
*********************************************************/
#alias {.resume}
{
        #var resumeController $stoppedController;
        .triggerEvent onRequestResume;
} {1};

/*********************************************************
        Override
*********************************************************/
#alias {.clearControllers}
{
        #var stoppedController {};
        #var resumeController {};
        #var currentController none;
};

/**************************************************************
        Switches to a corpse donation controller
**************************************************************/
#alias {.goDonateCoffin}
{
        #if { $bot == 0 }
        {
                .mapAnchor;
        }; 
        #else
        {
                .stop;
        };

        #delay {corpse_sell} 
        {
                go morgue;
                donate coffin;
        } {1};

        #delay {corpse_return} 
        {
                #if { $bot == 0 }
                {
                        .return;
                }; 
                #else
                {
                        .resume;
                };
        } {2};
}

/**************************************************************
        Switches to a selling controller
**************************************************************/
#alias {.goSell}
{
        #if { "$currentController" != "none" }
        {
	        .stop;
        };
	
	#delay {shop_sell} 
	{
		gonum $shops[$whichShop];
		sell all;
	} {1};
	
	#delay {shop_return} 
	{
                #if { "$resumeController" != "none" }
                {
		        .resume;
                }
	} {2};
};

/**************************************************************
        Go On Patrol, by itself it just goes through the
        patrol list. If a parameter is added, it will over
        ride the current position and go patrolling there
**************************************************************/
#alias .goPatrolling
{
        .stop;

        #if { "%1" != "" }
        {
                #showme {<202> Doing a specific patrol. <088>};
                .addPatrol %1;
                #var currentPatrolID &{patrols[]};
                .renderPatrols;
                go $patrols[$currentPatrolID];
                #delay {doLoadPatrollStart} { .loadPatrol $patrols[$currentPatrolID]; }  {2.5};
                #delay {doPatrolStart} { .startPatrol; } {4};
        };
        #elseif { &{patrols[]} == 0 } 
        {
                #showme {<202> Patroller has no patrols type .addPatrol <notelist> <088>};
        };
        #else
        {
                #showme {<202> Going to patrol [$patrols[$currentPatrolID]]  <088>};
                go $patrols[$currentPatrolID];
                #delay {doLoadPatrollStart} { .loadPatrol $patrols[$currentPatrolID]; }  {2.5};
                #delay {doPatrolStart} { .startPatrol; } {4};
        };
};

/**************************************************************
        Go mining, stops whatever controller is running and
        gives active control to the miner
**************************************************************/
#alias {.goMining}
{
        .stop;

        #if { @isDigit{{%1}} }
        {
                #var miner[miningLevel] %1;
        }

        go mines;
        #delay {doMiningStart} { .startMining; } {4};
};

/**************************************************************
        Redraw command
**************************************************************/
#alias {.render}
{
        #read 3k/TinTin/Core/Draw.tin
};

/**************************************************************

**************************************************************/
#alias {.onLoadCompleted}
{
        #showme ON LOAD COMPLETE;
        .render;
}

/**************************************************************
        
**************************************************************/
#alias {.calc} 
{
        #math {math} {%1};
        #format {math_format} {%g} {$math};
        #echo $math_format
}

/**************************************************************
        Generic alias        
**************************************************************/
#alias {ga} {get all}
#alias {gac} {get all from corpse}
#alias {gc} {get coins}
#alias {k} {kill}
#alias {.run} 
{
        #config speedwalk on;
        %0;
        #config speedwalk off
}

/**************************************************************
        Testing graveyard here
**************************************************************/
#alias {findme} 
{
        #map go $room ($exits);
};

#alias {nouns} 
{
    #script {python3 3k/TinTin/Tools/nountest.py "$roomdesc"};
};

#class {aliases} {close}
