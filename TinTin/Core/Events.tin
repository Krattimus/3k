#showme READING EVENTS.TIN
#class {events} {open}

#screen get rows screen[rows];
#screen get cols screen[cols];
#var screen[scroll] line;

#alias .captureUntil
{
	#var capturedText {};	
	#var renderingOff 1;
	#read 3k/TinTin/Core/CaptureAll.tin;
	#delay {create_capture} 
	{
		#ACTION {%1} 
		{
			#var renderingOff 0;
			#delay {0} {%2;};
			#CLASS {capture} {kill};
			#UNACTION {%1};
		} {1};
	} {0};
};

#alias .registerEvent
{
	#list %1 ADD %2;
};

#alias .unregisterEvent
{
	#if { &{%1} != 0 }
	{
		#list %1 {find} {%2} {lastID};
		
		#while { "$lastID" != "0" }
		{
			#list %1 DELETE $lastID;
			#list %1 {find} {%2} {lastID};
		};
	};
};

#alias {.doTrigger}
{
	#if { &{%1} != 0 }
	{
		#local list ${%1};
		#foreach {$list[%*]} {callback} 
		{
			#if {"%2" == ""}
			{
				#var makeCall $callback;
				$makeCall;
			};
			#else
			{
				#var makeCall $callback {%2};
				$makeCall;
			};
		};
	};
};

#alias {.triggerEvent}
{
	#math {endIndex} {@strlen{%1}-2};
	#local name @substr{{%1};{2};{$endIndex}};
	
	#format preCall {onPre%s} {$name};
	#format call {on%s} {$name};
	#format postCall {onPost%s} {$name};

	
	.doTrigger $preCall {%2};
	.doTrigger $call {%2};
	.doTrigger $postCall {%2};
};

/* Screen Sizing */
#event {SCREEN RESIZE} 
{
	#var screen[rows] %0;
	#var screen[cols] %1;
	#var screen[height] %2;
	#var screen[width] %3;
    .render;
};

#event {SCROLLED MOUSE WHEEL UP} 
{
	#if {%0 > $screen[rows] - $split[bottom]} 
	{
		#cursor {HISTORY PREV};
	};
	#if {%0 > $split[top] && %0 < $screen[rows] - $split[bottom]} 
	{
		#if {"$screen[scroll]" == "page"} 
		{
			#buffer up $screen[rows]
		}; 
		#elseif {"$screen[scroll]" == "line"} 
		{
			#buffer up 1
		};
	};
	#if {%0 < $split[top]} 
	{
		chatup
	};
};

#event {SCROLLED MOUSE WHEEL DOWN} 
{
	#if {%0 > $screen[rows] - $split[bottom]} 
	{
		#cursor {HISTORY NEXT};
	};

	#if {%0 > $split[top] && %0 < $screen[rows] - $split[bottom]} 
	{
		#if {"$screen[scroll]" == "page"} 
		{
			#buffer down $screen[rows]
		}; 
		#elseif {"$screen[scroll]" == "line"} 
		{
			#buffer down 1
		}
	};

	#if {%0 < $split[top]} 
	{
		chatdown
	}
};

#event {SESSION DISCONNECTED} 
{
	#NOP Save Map vnum per world;
	#map get roomvnum {map[$ourCharacter][position]};
	#NOP Save Map (updated exits for unpause);
	#NOP #map write .tt/map/3k.map;
	mapsave;
	#NOP Write var classes;
	#class patrolvar write 3k/TinTin/Data/Patrol.var;
	#class botvar write 3k/TinTin/Data/Bots.var;
	#class mapvar write 3k/TinTin/Data/Map.var;
	#class chatvar write 3k/TinTin/Data/3kchat.var;
	#class farmvar write 3k/TinTin/Data/KnownFarms.var;	
	#class pubvar write 3k/TinTin/Data/KnownPubs.var;
	#class shopvar write 3k/TinTin/Data/KnownShops.var;
	#class itemsvar write 3k/TinTin/Data/KnownItems.var;
	#class mobsvar write 3k/TinTin/Data/KnownMobs.var;
};

/* Map mouse clicks*/
#event {MAP UPDATED VTMAP} 
{
	.triggerEvent onMapUpdated;
};

#NOP Display room info on single-click
#event {MAP SHORT-CLICKED MOUSE BUTTON ONE} 
{
	#showme RECEIVED!;
	#map at %0 #map info
} {1};

#NOP Map goto room on double-click
#event {MAP DOUBLE-CLICKED MOUSE BUTTON ONE} 
{
	#map goto %0
};

#NOP Map run on right-click
#event {MAP SHORT-CLICKED MOUSE BUTTON THREE} 
{
	#map run %0
};

#EVENT {RECEIVED INPUT} 
{
	#var idle 0;
};


#class {events} {close}
