#showme READING Base.TIN
#class {base} {open}

#action {Leufarr whispers: The watchword is: %1} { #var watchword %1; }
/**************************************************************
	Calculator
**************************************************************/


#var dirs[n] overhere;
#var dirs[s] thatway;
#var dirs[e] yonder;
#var dirs[w] thisway;
#var dirs[u] anotherway;
#var dirs[d] overthere;

#alias {.n} { #send !$dirs[n]; #map move n; };
#alias {.s} { #send !$dirs[s]; #map move s; };
#alias {.e} { #send !$dirs[e]; #map move e; };
#alias {.w} { #send !$dirs[w]; #map move w; };
#alias {.u} { #send !$dirs[u]; #map move u; };
#alias {.d} { #send !$dirs[d]; #map move d; };

#alias {.hellmap} 
{ 
	gl;
	#delay {2}
	{
		#var exits2 $exits;

		#map get {roomcolor} {color};
		#map set {roomcolor} {<088>};
		#map set {roomid} {1};

		#replace {exits2} {$dirs[n]} {north};
		#replace {exits2} {$dirs[e]} {east};
		#replace {exits2} {$dirs[w]} {west};
		#replace {exits2} {$dirs[s]} {south};
		#replace {exits2} {$dirs[u]} {up};
		#replace {exits2} {$dirs[d]} {down};

		#replace exits2 {,} {;};
		#foreach {$exits2} {eachexit} 
		{
			#switch {"$eachexit"} 
			{
				#case {"north"}		{#map dig n};
				#case {"east"}		{#map dig e};
				#case {"south"}		{#map dig s};
				#case {"west"}		{#map dig w};
				#case {"northwest"}	{#map dig nw};
				#case {"northeast"}	{#map dig ne};
				#case {"southeast"}	{#map dig se};
				#case {"southwest"}	{#map dig sw};
				#case {"up"}		{#map dig u};
				#case {"down"}		{#map dig d};
				#default		{#map dig $eachexit}
			}
		};

		#map get {roomexit} {rexit};
		#foreach {$rexit} {eachexit} 
		{
			#map at {$eachexit} 
			{
				#map get {roomid} {rid};
				#if { "$rid" != "1" }
				{
					#map set {roomarea} Hell;
					#map set {roomcolor} {<118>}
				};
			}
		}
	};

} {1};


#alias .dir
{
	#var exits2 $exits;
	#replace {exits2} {$dirs[n]} {north};
	#replace {exits2} {$dirs[e]} {east};
	#replace {exits2} {$dirs[w]} {west};
	#replace {exits2} {$dirs[s]} {south};
	#replace {exits2} {$dirs[u]} {up};
	#replace {exits2} {$dirs[d]} {down};

	#draw Yellow scroll line 1 1 1 80;
	#showme Exits ($exits2);
	#draw Yellow scroll line 1 1 1 80;
};

/*************************************************************************
	Calculator util
*************************************************************************/
#alias {.calc} 
{
        #math {math} {%1};
        #format {math_format} {%g} {$math};
        #echo $math_format
}

/*************************************************************************
	Captures all text for a period of time
*************************************************************************/
#alias .timedCapture
{
	#var capturedText {};	
	#read 3k/TinTin/Core/CaptureAll.tin;

	#delay {create_capture} 
	{
		#ACTION {%%1} 
		{
			#delay {0} {%2;};
			#CLASS {capture} {kill};
			#UNACTION {%%1};
		} {1};
	} {%1};
};

/*************************************************************************
	Saves our data to file
*************************************************************************/
#alias {.save}
{
	#var char[currentRoomID] $currentRoomID;
	#var char[area] $area;
	#class chatvar write 3k/TinTin/Data/3kchat.var;
	#class farmvar write 3k/TinTin/Data/KnownFarms.var;	
	#class pubvar write 3k/TinTin/Data/KnownPubs.var;
	#class shopvar write 3k/TinTin/Data/KnownShops.var;
	#class itemsvar write 3k/TinTin/Data/KnownItems.var;
	#class mobsvar write 3k/TinTin/Data/KnownMobs.var;
	#class areasvar write 3k/TinTin/Data/KnownAreas.var;
	#class notessvar write 3k/TinTin/Data/KnownNotes.var;
	#class charvar write 3k/TinTin/Data/Character.var;
};

/*************************************************************************
	Switches to a new guild or character:
	.switchTo Monk Krat
*************************************************************************/
#alias {.switchTo}
{
        #var char[guild] %1;
        #var char[ourCharacter] %2;
        .save;
        #end;
};

/*************************************************************************
	Captures all text for a period of time... 
*************************************************************************/
#alias .captureFor 
{
	#class inventoryhelper kill;

	#var capturedText {};	
	#read 3k/TinTin/Core/CaptureAll.tin;
	#delay {backup_kill}
	{
		#CLASS {capture} {kill};
		#delay {0} {%2;};
	} {%1};
};

/*************************************************************************
	Captures all text until a grep is matched
*************************************************************************/
#alias .captureUntil
{
	#class inventoryhelper kill;

	#var capturedText {};	
	#read 3k/TinTin/Core/CaptureAll.tin;
	#delay {backup_kill}
	{
		#UNACTION {%1};
	} {2};

	#delay {create_capture} 
	{
		#ACTION {%1} 
		{
			#delay {0} {%2;};
			#CLASS {capture} {kill};
			#UNACTION {%1};
		} {1};
	} {0};
};

/*************************************************************************
	Quickly reloads everything in the test bin
*************************************************************************/
#alias .testBin
{
	#class {testbin} {kill};
	#read 3k/TinTin/Core/TestBin.tin;
};

/**************************************************************
        Renders the display
**************************************************************/
#alias {.render}
{
        #read 3k/TinTin/Core/Draw.tin
};

/**************************************************************

**************************************************************/
#alias {.onLoadCompleted}
{
        #showme ON LOAD COMPLETE;
        .render;
};

/**************************************************************
	Search look, calls both
**************************************************************/
#alias {sl %0} 
{
	#send look at %0;
	#send search %0;
};

/**************************************************************
	Search Room Nouns (built by MIP)
**************************************************************/
#alias {sn} 
{
	#var noun_cnt &{nouns[]};
	#loop 1 &{nouns[]} cnt 
	{
		#showme Searching Noun [$nouns[$cnt]];
		look at $nouns[$cnt];
		search $nouns[$cnt];
	};
	#delay 0.5 
	{
		#if {$noun_cnt < &{nouns[]}} 
		{
			#echo {<128>New items found!  Search again!<088>}
		}
	}
};

/**************************************************************
	Adds a highlight to the reps output for readability
**************************************************************/
#alias .rep 
{
        #if { "%0" == "" }
        {
                reps;
        };
        #else
        {
                #highlight {%%i%0%%*\|%%*} {<338>};reps;

                #delay {2} 
                {
                        #unhighlight {%%i%0%%*\|%%*} {<338>};
                } {1};
        };
};



#alias {.cls}
{
    #buffer clear;
    .render;
};

#class {base} {close}
