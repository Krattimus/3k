#showme READING Joystick
#class {joystick} {open}

#var joystick[leftThumbstick][radius] 0; /* Left Tumbstick: X axis */
#var joystick[rightThumbstick][angle] 0; /* Left Tumbstick: Y axis, -1 north */

#var joystick[nounIndex] 0;
#var joystick[button][0] 0;
#var joystick[button][1] 0;
#var joystick[button][2] 0;
#var joystick[button][3] 0;
#var joystick[button][4] 0;
#var joystick[button][5] 0;
#var joystick[button][6] 0;
#var joystick[button][7] 0;
#var joystick[button][8] 0;
#var joystick[button][9] 0;
#var joystick[button][10] 0;
#var joystick[button][11] 0;
#var joystick[button][12] 0;
#var joystick[button][13] 0;
#var joystick[button][14] 0;
#var joystick[button][15] 0;
#var joystick[button][16] 0;

#var joystick[didEdge] 0;

#var joystick[action][0] search;
#var joystick[action][1] get all;
#var joystick[action][2] get all;
#var joystick[action][3] look;
#var joystick[action][4] #var nouns;
#var joystick[action][5] 0;
#var joystick[action][6] consider;
#var joystick[action][7] kill $event[foundMobName];
#var joystick[action][8] showJoystickMapping;
#var joystick[action][9] 0;
#var joystick[action][10] .go cot;
#var joystick[action][11] portal;
#var joystick[action][12] enter;
#var joystick[action][13] i;
#var joystick[action][14] gs;
#var joystick[action][15] .map;
#var joystick[action][16] 0;

/**************************************************************
	Testing ideas of "talking" to web servers
**************************************************************/
#alias {.startJoyStickListener}
{
	#port init joyStickListener 3333;
	#EVENT {PORT RECEIVED MESSAGE} { #krat %%3 };
	#session krat;
	.render;
};

/**************************************************************
    Joy stick messages start with =j= for easier parsing.
        With 2 parameters, this is a button press.
        =j= <buttonID> <on or off>
**************************************************************/
#alias {=j= %1 %2}
{	
	#if { $joystick[button][%1] == 1 && %2 == 0 }
	{
		#if { %1 == 7 }
		{
			#send kill $event[foundMobName];
		};
		#else
		{
			$joystick[action][%1];
		};
	};
	
	#var joystick[button][%1] %2;
};

/**************************************************************
    Joy stick messages start with =j= for easier parsing.
        With 4 parameters, this is a thumb stick / axis
**************************************************************/
#alias {=j= %1 %2 %3 %4}
{	
	#var joystick[axis][%1] %2;
	#local axisName none;
	#if { %1 == 0 || %1 == 1 }
	{
		#local axisName leftThumbstick;
	};

	#if { %1 == 3 || %1 == 4 }
	{
		#local axisName rightThumbstick;
	};


    #local newRadius %3;
    #local newAngle %4;

    #if { $newRadius > 0.6 }
    {
        #var joystick[didEdge] 10;
    };

    #math {joystick[didEdge]} {$joystick[didEdge]-1};


    #if { $newRadius <= 0.01 && $newAngle == 45.0 }
    {
        #local oldRadius $joystick[$axisName][radius];
        #if { $joystick[didEdge] > 0 }
        {
            .doJoystickMovement $joystick[$axisName][angle];
        };

        #var joystick[didEdge] 0;
    };


	#var joystick[$axisName][radius] $newRadius;
	#var joystick[$axisName][angle] $newAngle;
} {4};



#alias {.doJoystickMovement}
{
    #var angle %1;

    #if { $angle < -84 && $angle > -98 }
    {
        #showme <274>Moving North<088>;
        #send n;
    };

    #if { $angle > 85 && $angle < 92 }
    {
        #showme <274>Moving South<088>;
        #send s;
    };

    #if { $angle > 174 && $angle < 180 }
    {
        #showme <274>Moving West<088>;
        #send w;
    };

    #if { $angle > -5 && $angle < 5 }
    {
        #showme <274>Moving East<088>;
        #send e;
    };
    #if { $angle < -30 && $angle > -55 }
    {
        #showme <274>Moving Northeast<088>;
        #send ne;
    };

    #if { $angle < -125 && $angle > -145 }
    {
        #showme <274>Moving Northwest<088>;
        #send nw;
    };

    #if { $angle < 160 && $angle > 120 }
    {
        #showme <274>Moving Southwest<088>;
        #send sw;
    };

    #if { $angle < 70 && $angle > 40 }
    {
        #showme <274>Moving Southeast<088>;
        #send se;
    };
};

/**************************************************************
    Thrills & Giggles
**************************************************************/
#alias showJoystickMapping
{

    #draw Yellow scroll line 1 1 1 80;
    #showme <088>              <139>9<088>        <139>10<088>                            <139>11<088>          <139>12<088> ;
	#showme <088>                _=====_                               _=====_ ;
	#showme <088>               / _____ \\                             / _____ \\ ;
	#showme <088>             +.-'_____'-.---------------------------.-'_____'-.+ ;
	#showme <088>            /   |     |  '.                       .'  |  _  |   \\ ;
	#showme <088>           / ___| /|\\ |___ \\                     / ___| /<139>7<088>\\ |___ \\ ;
	#showme <088>          / |      <139>1<088>      | |  __           _   | | _         _ | | ;
	#showme <088>          | | < <139>2<088>     <139>4<088> > | | |__|         |_:> | ||<139>5<088>|       (<139>8<088>)| | ;
	#showme <088>          | |___   <139>3<088>   ___| |                   | |___       ___| | ;
	#showme <088>          |\\    | \\|/ |    /  _     ___      _   \\    | (<139>6<088>) |    /| ;
	#showme <088>          | \\   |_____|  .','" "', |___|  ,'" "', '.  |_____|  .' | ;
	#showme <088>          |  '-.______.-' /       \\      /       \\  '-._____.-'   | ;
	#showme <088>          |               |       |------|       |                | ;
	#showme <088>          |              /\\       /      \\       /\\               | ;
	#showme <088>          |             /  '.___.'        '.___.'  \\              | ;
	#showme <088>          |            /                            \\             | ;
	#showme <088>           \\          /                              \\           / ;
	#showme <088>            \\________/                                \\_________/ ;
	#draw Yellow scroll line 1 1 1 80;
    #echo {<129>    };
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>1<088>} {<129>DPad Up<088>} {$joystick[action][13]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>2<088>} {<129>DPad Left<088>}{$joystick[action][15]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>3<088>} {<129>DPad Down<088>}{$joystick[action][14]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>4<088>} {<129>DPad Right<088>}{$joystick[action][16]};
    #echo {<129>    };
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>5<088>} {<129>Square<088>}{$joystick[action][3]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>6<088>} {<129>X<088>}{$joystick[action][0]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>7<088>} {<129>Triangle<088>}{$joystick[action][1]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>8<088>} {<129>Circle<088>}{$joystick[action][2]};
    #echo {<129>    };
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>9<088>} {<129>Left Top Trigger<088>}{$joystick[action][4]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>10<088>} {<129>Left Bottom Trigger<088>} {$joystick[action][6]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>11<088>} {<129>Right Top Trigger<088>} {$joystick[action][5]};
    #echo { [%-2s] %-20s <268>%.30s<088>} {<139>12<088>} {<129>Right Bottom Trigger<088>}{$joystick[action][7]};
    #echo {<129>    };
    #draw Yellow scroll line 1 1 1 80;
    
};


#class {joystick} {close}
