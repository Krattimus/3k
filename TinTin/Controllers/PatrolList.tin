#showme READING patrollist

/***********************************************************************************
	Manages a list of patrols that patrol bot can string in a sequence,
	the list is nothing more than an array of notelist areas that have an
	area file in Areas, Current limitation is that there can only be 1 patrol
	per starting notelist
***********************************************************************************/
#class {patrollist} {open}

#alias {.listPatrols} 
{
	#echo {<228> +-------------------+}<088>;
	#echo {<228> |<138>    Patrols    <228>|<088>};
	#echo {<228> +-------------------+}<088>;
	
	#if {&{patrols[]} == 0} 
	{
		#echo {<228> |<178>  No patrols  <228>|<088>};
		#echo {<228> +-------------------+}<088>
	};
	#else
	{
		#var cnt 0;
		#while {$cnt < &{patrols[]}} 
		{
			#math {cnt} {$cnt + 1};
			#if {$cnt == $currentPatrolID} 
			{
				#format {cntnum} {%+4s} {<128>$cnt\.<088>};
				#format {area_display} {%-13s} {<128>$patrols[$cnt]<088>}
			};
			#else
			{
				#format {cntnum} {%+4s} {<178>$cnt\.<088>};
				#format {area_display} {%-13s} {<148>$patrols[$cnt]<088>}
			};
			
			#echo { <228>| $cntnum $area_display <228>|<088>};
		};
		#echo { <228>+-------------------+}<088>
	}
}

#alias {.nextPatrol}
{
	#if {$currentPatrolID < &{patrols[]}} 
	{
		#math currentPatrolID {$currentPatrolID+1}
	};
	#else
	{
		#var currentPatrolID 1
	};
	
}

#alias {.addPatrol}
{
	#if {&{patrols[]} == 0} 
	{
		#var currentPatrolID 1;
	};

	#list {patrols} {add} {%1};
}

#alias {.removePatrol}
{
	#list {patrols} {del} {%1};
	#if {%1 < $currentPatrolID} 
	{
		#math {currentPatrolID} {$currentPatrolID - 1}
	};
}

#class {patrollist} {close}
